// add rest.li's gradle plugins so they can be used throughout project
buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath 'com.linkedin.pegasus:gradle-plugins:2.2.5'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}

final pegasusVersion = '2.2.5'
ext.spec = [
  'product' : [
    'pegasus' : [
      'data' : 'com.linkedin.pegasus:data:' + pegasusVersion,
      'generator' : 'com.linkedin.pegasus:generator:' + pegasusVersion,
      'restliCommon' : 'com.linkedin.pegasus:restli-common:' + pegasusVersion,
      'restliClient' : 'com.linkedin.pegasus:restli-client:' + pegasusVersion,
      'restliServer' : 'com.linkedin.pegasus:restli-server:' + pegasusVersion,
      'restliTools' : 'com.linkedin.pegasus:restli-tools:' + pegasusVersion,
      'gradlePlugins' : 'com.linkedin.pegasus:gradle-plugins:' + pegasusVersion,
      'restliNettyStandalone' : 'com.linkedin.pegasus:restli-netty-standalone:' + pegasusVersion,
      'restliServerStandalone' : 'com.linkedin.pegasus:restli-server-standalone:' + pegasusVersion
    ]
  ]
]

project.ext.externalDependency = [
  'slf4jApi': 'org.slf4j:slf4j-api:1.6.2',
  'slf4jLog4j2': 'org.apache.logging.log4j:log4j-slf4j-impl:2.3',
  'slf4jLog4j2core': 'org.apache.logging.log4j:log4j-core:2.3',
  'dropbox_core_sdk': 'com.dropbox.core:dropbox-core-sdk:1.7.7',
  'aws_java_sdk_ses': 'com.amazonaws:aws-java-sdk-ses:1.9.30',
  'aws_java_sdk_core': 'com.amazonaws:aws-java-sdk-core:1.9.30',
  'aws_java_sdk': 'com.amazonaws:aws-java-sdk:1.9.30',
  'twilio_sdk': 'com.twilio.sdk:twilio-java-sdk:3.4.6',
  'kafka': 'org.apache.kafka:kafka_2.10:0.8.1.1',
  'apache_httpclient': 'org.apache.httpcomponents:httpclient:4.2.6',
  'apache_httpcore': 'org.apache.httpcomponents:httpcore:4.2.5'
];

allprojects {
  apply plugin: 'idea'
  apply plugin: 'eclipse'
}

subprojects {
  apply plugin: 'maven'

  afterEvaluate {
    if (project.plugins.hasPlugin('java')) {
      sourceCompatibility = JavaVersion.VERSION_1_7
    }

    // add the standard pegasus dependencies wherever the plugin is used
    if (project.plugins.hasPlugin('pegasus')) {
      dependencies {
        dataTemplateCompile spec.product.pegasus.data
        restClientCompile spec.product.pegasus.restliClient

        // needed for Gradle 1.9+
        restClientCompile spec.product.pegasus.restliCommon
      }
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }
}
